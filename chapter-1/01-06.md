(define (new-if predicate consequent alternative)
  (cond (predicate consequent)
        (else alternative)))

(define (square x)
  (* x x))

(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))

(define (average x y)
  (/ (+ x y) 2))

(define (improve guess x)
  (average guess (/ x guess)))

(define (sqrt-iter guess x)
  (new-if (good-enough? guess x)
          guess
          (sqrt-iter (improve guess x)
                     x)))

(define (sqrt x)
  (sqrt-iter 1.0 x))

Let's see what happens when we apply the evaluation rules manually:

The rules are:
* [1] **Evaluate** all the subexpressions of the combination (in any order).
* [2] **Apply** the value of the first subexpression to the values of all the other subexpressions.

* [3] If the procedure to apply is a *primitive*, just do it.
* [4] If the procedure is a *compound procedure*, **evaluate** the body of the procedure with each formal parameter replaced by the corresponding actual argument expression value.

;; #4
(sqrt 4)

;; #4
(sqrt-iter 1.0 4)

;; #4
(new-if (good-enough? 1.0 4)
          1.0
          (sqrt-iter (improve 1.0 4)
                     4))

;; #4
(new-if false
	1.0
	(sqrt-iter (improve 1.0 4)
		    4))
;; #4
(new-if false
	1.0
	(sqrt-iter 2.5 4))

;; #4
(new-if false
	1.0
	(new-if (good-enough? 2.5 4)
		2.5
		(sqrt-iter (improve 2.5 4) 4)))

;; #4
(new-if false
	1.0
	(new-if false
		2.5
		(sqrt-iter (improve 2.5 4) 4)))

;; #4
(new-if false
	1.0
	(new-if false
		2.5
		(sqrt-iter 2.05 4)))

;; And the process repeats itself forever because new-if is a procedure and not a cond.

